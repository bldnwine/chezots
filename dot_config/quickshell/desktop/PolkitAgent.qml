import QtQuick
import Quickshell
import Quickshell.Io
import Quickshell.Services.Polkit
import Quickshell.Wayland
import "PolkitModel.js" as PolkitModel

Item {
    id: root

    required property var theme

    readonly property string fontFamily: theme.mono
    readonly property color accent: theme.indigo
    readonly property color background: theme.bg
    readonly property color foreground: theme.ink
    readonly property color border: theme.sep
    readonly property color borderError: theme.seal
    readonly property color textError: theme.seal
    readonly property color scrim: Qt.rgba(theme.paper.r, theme.paper.g, theme.paper.b, 0.65)
    readonly property int cornerRadius: theme.cornerRadius

    readonly property int cardWidth: 320
    readonly property int cardHeight: 52

    property bool closing: false
    property bool submitted: false
    property string currentMessage: ""
    property string currentPrompt: ""
    property string currentSupplementary: ""
    property bool responseRequired: false
    property bool responseVisible: false
    property bool failed: false
    property bool errorFlash: false
    property bool fingerprintFirst: false
    property int shakeOffset: 0

    readonly property bool dialogVisible: polkitAgent.isActive || closing
    readonly property bool fingerprintWaiting: dialogVisible && !responseRequired && !submitted && (fingerprintFirst || promptLooksFingerprint(currentPrompt + " " + currentSupplementary))

    function promptLooksFingerprint(text) {
        return PolkitModel.promptLooksFingerprint(text);
    }

    function loadPamConfig(raw) {
        fingerprintFirst = PolkitModel.fingerprintFirstFromPamConfig(raw);
    }

    function resetSnapshot() {
        currentMessage = "";
        currentPrompt = "";
        currentSupplementary = "";
        responseRequired = false;
        responseVisible = false;
        failed = false;
        errorFlash = false;
        submitted = false;
        passwordInput.text = "";
    }

    function syncFromFlow() {
        var flow = polkitAgent.flow;
        if (!flow) return;

        currentMessage = String(flow.message || "Authentication is needed...");
        currentPrompt = String(flow.inputPrompt || "");
        currentSupplementary = String(flow.supplementaryMessage || "");
        responseRequired = !!flow.isResponseRequired;
        responseVisible = !!flow.responseVisible;
        failed = !!flow.failed;

        if (responseRequired) submitted = false;
    }

    function beginFlow() {
        closeTimer.stop();
        closing = false;
        submitted = false;
        passwordInput.text = "";
        syncFromFlow();
        Qt.callLater(refocus);
    }

    function refocus() {
        if (!dialogVisible) return;
        if (fingerprintWaiting) keyCatcher.forceActiveFocus();
        else passwordInput.forceActiveFocus();
    }

    function submitResponse() {
        var flow = polkitAgent.flow;
        if (!flow || !flow.isResponseRequired) return;
        submitted = true;
        errorFlash = false;
        flow.submit(passwordInput.text);
        passwordInput.text = "";
        keyCatcher.forceActiveFocus();
    }

    function cancelRequest() {
        var flow = polkitAgent.flow;
        passwordInput.text = "";
        submitted = false;
        closing = true;
        closeTimer.restart();
        if (flow) flow.cancelAuthenticationRequest();
    }

    function triggerFailureFeedback() {
        submitted = false;
        errorFlash = true;
        passwordInput.text = "";
        errorTimer.restart();
        shakeAnimation.restart();
        Qt.callLater(refocus);
    }

    Timer {
        id: closeTimer
        interval: 300
        repeat: false
        onTriggered: {
            root.closing = false;
            root.resetSnapshot();
        }
    }

    Timer {
        id: errorTimer
        interval: 1200
        repeat: false
        onTriggered: root.errorFlash = false
    }

    SequentialAnimation {
        id: shakeAnimation
        NumberAnimation { target: root; property: "shakeOffset"; to: -8; duration: 35; easing.type: Easing.OutQuad }
        NumberAnimation { target: root; property: "shakeOffset"; to: 8; duration: 50; easing.type: Easing.InOutQuad }
        NumberAnimation { target: root; property: "shakeOffset"; to: 0; duration: 55; easing.type: Easing.OutQuad }
    }

    FileView {
        path: "/etc/pam.d/polkit-1"
        watchChanges: true
        printErrors: false
        onLoaded: root.loadPamConfig(text())
        onLoadFailed: root.fingerprintFirst = false
        onFileChanged: reload()
    }

    PolkitAgent {
        id: polkitAgent
        path: "/org/omarchy/PolkitAgent"

        onAuthenticationRequestStarted: root.beginFlow()
        onIsActiveChanged: {
            if (isActive) root.syncFromFlow();
            else if (!root.closing) root.resetSnapshot();
        }
        onIsRegisteredChanged: {
            if (isRegistered) console.log("quickshell polkit agent registered");
            else console.warn("quickshell polkit agent is not registered; another agent may be running");
        }
    }

    Connections {
        target: polkitAgent.flow

        function onIsResponseRequiredChanged() {
            root.syncFromFlow();
            if (!polkitAgent.flow || !polkitAgent.flow.isResponseRequired) passwordInput.text = "";
            Qt.callLater(root.refocus);
        }

        function onInputPromptChanged() { root.syncFromFlow(); }
        function onResponseVisibleChanged() { root.syncFromFlow(); }
        function onSupplementaryMessageChanged() { root.syncFromFlow(); }
        function onFailedChanged() { root.syncFromFlow(); }

        function onAuthenticationFailed() {
            root.syncFromFlow();
            root.triggerFailureFeedback();
        }

        function onAuthenticationSucceeded() {
            root.closing = true;
            closeTimer.restart();
        }

        function onAuthenticationRequestCancelled() {
            root.closing = true;
            closeTimer.restart();
        }
    }

    PanelWindow {
        id: panel
        visible: root.dialogVisible
        anchors { top: true; bottom: true; left: true; right: true }
        color: "transparent"
        WlrLayershell.namespace: "omarchy-polkit"
        WlrLayershell.layer: WlrLayer.Overlay
        WlrLayershell.keyboardFocus: WlrKeyboardFocus.Exclusive
        exclusionMode: ExclusionMode.Ignore

        Rectangle {
            anchors.fill: parent
            color: root.scrim
        }

        MouseArea {
            anchors.fill: parent
            onClicked: root.refocus()
        }

        Rectangle {
            id: card
            width: root.cardWidth
            height: root.cardHeight
            radius: root.cornerRadius
            anchors.centerIn: parent
            anchors.horizontalCenterOffset: root.shakeOffset
            color: root.background
            border.color: root.errorFlash ? root.borderError : root.border
            border.width: 1

            MouseArea {
                anchors.fill: parent
                onClicked: root.refocus()
            }

            Item {
                id: keyCatcher
                anchors.fill: parent
                focus: true

                Keys.priority: Keys.BeforeItem
                Keys.onPressed: function(event) {
                    if (event.key === Qt.Key_Escape) {
                        root.cancelRequest();
                        event.accepted = true;
                    } else if (event.key === Qt.Key_Return || event.key === Qt.Key_Enter) {
                        if (root.responseRequired) root.submitResponse();
                        event.accepted = true;
                    }
                }
            }

            Row {
                id: cardRow
                anchors.fill: parent
                anchors.leftMargin: 16
                anchors.rightMargin: 16
                spacing: 12

                Text {
                    text: "\uf023"
                    color: root.errorFlash ? root.textError : root.accent
                    font.family: root.fontFamily
                    font.pixelSize: 18
                    width: 24
                    height: parent.height
                    horizontalAlignment: Text.AlignHCenter
                    verticalAlignment: Text.AlignVCenter
                }

                Item {
                    width: parent.width - 36
                    height: parent.height

                    TextInput {
                        id: passwordInput
                        anchors.fill: parent
                        verticalAlignment: TextInput.AlignVCenter
                        activeFocusOnPress: true
                        clip: true
                        selectionColor: Qt.rgba(root.accent.r, root.accent.g, root.accent.b, 0.35)
                        selectedTextColor: root.foreground
                        font.family: root.fontFamily
                        font.pixelSize: 14
                        echoMode: root.responseVisible ? TextInput.Normal : TextInput.Password
                        passwordCharacter: "\u2022"
                        color: root.errorFlash ? root.textError : root.foreground
                        cursorVisible: activeFocus && !root.submitted && !root.errorFlash
                        readOnly: root.submitted || root.errorFlash
                        enabled: root.dialogVisible && !root.fingerprintWaiting
                        visible: !root.fingerprintWaiting
                        onAccepted: root.submitResponse()
                        Keys.onPressed: function(event) {
                            if (event.key === Qt.Key_Escape) {
                                root.cancelRequest();
                                event.accepted = true;
                            }
                        }
                    }

                    Text {
                        anchors.left: parent.left
                        anchors.right: parent.right
                        anchors.verticalCenter: parent.verticalCenter
                        text: root.errorFlash
                            ? "Wrong"
                            : (root.submitted
                                ? "Checking..."
                                : (root.fingerprintWaiting ? "Swipe fingerprint or enter password" : (root.currentPrompt || "Enter password")))
                        color: root.errorFlash ? root.textError : root.foreground
                        opacity: root.errorFlash ? 1 : 0.4
                        font.family: root.fontFamily
                        font.pixelSize: 14
                        elide: Text.ElideRight
                        visible: !passwordInput.visible || passwordInput.text.length === 0
                    }

                    Rectangle {
                        width: 2
                        height: 18
                        anchors.left: parent.left
                        anchors.verticalCenter: parent.verticalCenter
                        color: root.errorFlash ? root.textError : root.accent
                        visible: passwordInput.visible && passwordInput.activeFocus && passwordInput.text.length === 0 && !root.submitted && !root.errorFlash
                    }

                    MouseArea {
                        anchors.fill: parent
                        acceptedButtons: Qt.LeftButton
                        enabled: passwordInput.visible
                        onClicked: passwordInput.forceActiveFocus()
                    }
                }
            }
        }
    }
}
